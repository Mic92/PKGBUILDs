# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

pkgname=etherpad-lite-systemd
pkgver=1.4.0
pkgrel=2
pkgdesc="etherpad-lite with systemd socket-activation"
arch=(any)
url="http://etherpad.org"
_watch=('http://etherpad.org','Documentation <small>v([\d.]*)</small>')
license=('GPL2')
depends=('curl' 'python2' 'openssl' 'nodejs')
optdepends=('sqlite: to use sqlite as databse'
            'mariadb: to use mysql/mariadb as database'
            'postgresql: to use postgresql as database')
conflicts=('etherpad-lite-git')
backup=(usr/share/webapps/etherpad-lite/settings.json)
install='etherpad-lite.install'
source=("etherpad-lite-${pkgver}.tar.gz::https://github.com/ether/etherpad-lite/archive/${pkgver}.tar.gz"
  "etherpad-lite.service"
  "etherpad-lite.tmpfile.d"
  "etherpad-lite.socket"
  "socket-activation.patch")
sha512sums=('9f07b12ef5079d0fc02bb0ac3339b290501fef5fcb262ee109132cbb967506305aa6faef008d4694fb99f42b69f5aba9e3f4c6eb1865d7b269bbc6d00c313385'
  '5f1273a9effcc17c81cdc275a97c4f29ad6145f42969fb1c06d5e963300908717d018cc2c0ce26a5b1ad8b86dfea6dd9159dd528a6585307c4079b5b4417b0d6'
  '8172f4fc2d1f394046cb9215b50917cc5f42559d030b408c5c1919e3b62e53776d56ec00674260a332533545aa08160993e8c61663ce2786c9d97a2dafd560e4'
  'SKIP'
  'SKIP')

prepare() {
  cd "$srcdir/etherpad-lite-${pkgver}"
  sed -i "s/9001/19001/" settings.json.template
  patch -p0 -i "$srcdir/socket-activation.patch"
}

build() {
  cd "$srcdir/etherpad-lite-${pkgver}"
  ./bin/installDeps.sh
}

package() {
  cd "$srcdir/etherpad-lite-${pkgver}"
  rm -r start.bat var
  install -dm 755 "${pkgdir}"/usr/share/webapps/etherpad-lite
  cp -a . "${pkgdir}"/usr/share/webapps/etherpad-lite
  install -Dm644 "${srcdir}"/etherpad-lite.service "${pkgdir}"/usr/lib/systemd/system/etherpad-lite.service
  install -Dm644 "${srcdir}"/etherpad-lite.socket "${pkgdir}"/usr/lib/systemd/system/etherpad-lite.socket
  install -Dm644 "${srcdir}"/etherpad-lite.tmpfile.d "${pkgdir}"/usr/lib/tmpfiles.d/etherpad-lite.conf
  install -D LICENSE "${pkgdir}"/usr/share/licenses/etherpad-lite/LICENSE
}
