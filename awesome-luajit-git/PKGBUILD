# Contributor: noonov <noonov@gmail.com>
# Contributor: wtchappell <wtchappell@gmail.com>

# Generate ldoc docs (yes/no)
_docs="no"

_pkgname=awesome
pkgname=awesome-luajit-git
pkgver=20130919
pkgrel=1
pkgdesc="awesome window manager build with luajit"
arch=('i686' 'x86_64')
url="http://awesome.naquadah.org/"
license=('GPL2')
depends=(
  'startup-notification' 'libxdg-basedir' 'dbus' 'luajit'
  'cairo>=1.12.2' 'pango' 'gdk-pixbuf2' 'luajit-lgi'
  'xcb-util'{,-keysyms,-wm}'>=0.3.8' 'xorg-xmessage' 'xcb-util-cursor-git')
makedepends=('git' 'cmake' 'asciidoc' 'xmlto' 'doxygen' 'imagemagick')
optdepends=(
  'rlwrap: readline support for awesome-client'
  'dex: autostart your desktop files'
  'vicious-git: widgets for the Awesome window manager')
provides=('awesome' 'notification-daemon')
conflicts=('awesome' 'awesome-git')
options=('docs')
install=${pkgname}.install
source=("git+http://git.naquadah.org/git/awesome.git"
        0001-add-luajit.patch
        awesome.desktop)
sha1sums=('SKIP'
          '0d419f4ec61226dbda0dd8026877cf0c7c8ad1fb'
          '27c318614e7e52be723de8a1cdec3297c8356a59')

if [[ ${_docs,,} == yes ]]; then
  makedepends+=('ldoc')
fi

pkgver() {
  cd ${srcdir}/${_pkgname}
  git log -1 --format='%cd' --date=short | tr -d -- '-'
}
 
build() {
  cd ${srcdir}/${_pkgname}
  export AWESOME_IGNORE_LGI=1
  git apply ${srcdir}/0001-add-luajit.patch
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_LIBRARIES=/usr/lib/libluajit-5.1.so \
    -DGENERATE_DOC=${_docs:=no}
  make
}

package() {
  cd ${srcdir}/${_pkgname}

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/awesome.desktop \
    ${pkgdir}/usr/share/xsessions/awesome.desktop
  install -D -m644 ${srcdir}/awesome.desktop \
    ${pkgdir}/usr/share/apps/ksmserver/windowmanagers/awesome.desktop
}
