pkgname=qemu-6828
_gitname=qemu
pkgver=2.3.0
pkgrel=1
pkgdesc="QEMU patched for MIT-6.828 course."
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2')
url="http://wiki.qemu.org/Index.html"
depends=('pixman' 'libjpeg' 'libpng' 'sdl' 'alsa-lib' 'nss' 'glib2'
         'gnutls>=2.4.1' 'bluez-libs' 'vde2' 'util-linux' 'curl' 'libsasl'
         'libgl' 'libpulse' 'libcap-ng' 'libaio' 'libseccomp'
         'libiscsi' 'spice' 'usbredir' 'dtc' 'snappy')
makedepends=('git' 'texi2html' 'perl' 'python2' 'spice-protocol')
optdepends=('ovmf: Tianocore UEFI firmware for qemu')
conflicts=('qemu' 'kvm' 'kvm-git' 'qemu-spice' 'libcacard' 'seabios' 'seabios-git')
provides=('qemu' 'qemu-kvm' 'qemu-spice' 'libcacard' 'seabios' 'seabios-git')
backup=('etc/qemu/target-x86_64.conf')
source=("git://github.com/Mic92/qemu.git#branch=6.828-${pkgver}"
        '65-kvm.rules'
        'qemu.install')
sha256sums=('SKIP'
            '9c8a15c34461a9481a34ca9e0ab4ae3825eabe8fd863227f2445325413cd755c'
            '9970c3050e8dc6153c5955d018d114f9fcbc091843b85f9e7b247eb28f09ba10')
install='qemu.install'
options=('!strip')

pkgver() {
	cd "${srcdir}/$_gitname"
	echo "2.3.r$(git rev-list --count master).g$(git log -1 --format="%h")"
}

build() {
	cd $_gitname
	# qemu vs. make 4 == bad
  	export ARFLAGS="rv"
  	# http://permalink.gmane.org/gmane.comp.emulators.qemu/238740
  	# gtk gui breaks keymappings at the moment
 	./configure --prefix=/usr --sysconfdir=/etc --audio-drv-list='pa alsa sdl' \
              --python=/usr/bin/python2 --smbd=/usr/bin/smbd \
              --enable-docs --libexecdir=/usr/lib/qemu \
              --disable-gtk --enable-linux-aio --enable-seccomp \
              --enable-spice --localstatedir=/var --disable-werror
  	make V=99
}

package() {
  cd $_gitname

  make DESTDIR="${pkgdir}" libexecdir="/usr/lib/qemu" install
  # remove conflicting /var/run directory
  rm -r "${pkgdir}/var"
  install -D -m644 "${srcdir}/65-kvm.rules" \
                   "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"
  # bridge_helper needs suid
  # https://bugs.archlinux.org/task/32565
  chmod u+s "${pkgdir}/usr/lib/qemu/qemu-bridge-helper"
  # add sample config
  echo "allow br0" > ${pkgdir}/etc/qemu/bridge.conf.sample
}
